*** Settings ***
Suite Setup    Run tests with options containing </script>
Resource       atest_resource.txt
Force Tags     pybot    jybot    regression

*** Test Cases ***
Strings like </script> in JS model are escaped
    Check test case    </script>
    Model should contain escaped strings    log
    Model should contain escaped strings    report

*** Keywords ***
Run tests with options containing </script>
    ${options} =    Catenate
    ...    --log log.html
    ...    --report report.html
    ...    --escape lt:LT
    ...    --escape gt:GT
    ...    --name LT/scriptGT
    ...    --logtitle LT/scriptGT
    ...    --reporttitle LT/scriptGT
    ...    --tagdoc *:LT/scriptGT
    ...    --tagstatlink LT/scriptGT:LT/scriptGT:LT/scriptGT
    ...    --tagstatcombine *:LT/scriptGT:LT/scriptGT
    Run tests    ${options}    output/js_model.txt

Model should contain escaped strings
    [Arguments]    ${type}
    ${strings}  ${settings} =   Get JS model    ${type}
    Should not contain    ${strings}    </script>
    Should contain    ${strings}    &lt;/script&gt;
    Should not contain    ${settings}    </script>
    Should contain    ${settings}    &lt;/script&gt;

Get JS model
    [Arguments]    ${type}
    ${file} =    Get File    ${OUTDIR}/${type}.html
    ${strings} =    Get Lines Matching Pattern    ${file}    window.output?"strings"?*
    ${settings} =    Get Lines Matching Pattern    ${file}    window.settings =*
    [Return]    ${strings}    ${settings}
